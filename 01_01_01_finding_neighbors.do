/*******************************************************************************

													IDENTIFYING NEIGHBORS

	Based on household coordinates this do-file identifies close neighbors
	of individuals registered for taking the PSU in year `x' among individuals
	registered for taking the PSU in year `y'.

	This code uses as input the files generated by the geocoding process. Using
	latitudes and longitudes finds close neighbors and generates datasets with
	potential applicants and their 50 closest neighbors.
*******************************************************************************/

forvalues t = -6/6 {

	if `t' == -6 local min = 2012
	if `t' == -5 local min = 2011
	if `t' == -4 local min = 2010
	if `t' == -3 local min = 2009
	if `t' == -2 local min = 2008
	if `t' == -1 local min = 2007

	if `t' >= 0  local min = 2006

	if `t' <= 0  local max = 2012
	if `t' == 1  local max = 2011
	if `t' == 2  local max = 2010
	if `t' == 3  local max = 2009
	if `t' == 4  local max = 2008
	if `t' == 5  local max = 2007
	if `t' == 6  local max = 2006

	forvalues x = `min'/`max' {

		local y = `x' + `t'

		use "${input_path}${dash}mc_`x'.dta", clear
		rename code code_o
		geonear code_o latitud longitud using "${input_path}${dash}mc_`y'.dta", n(code latitud longitud) ign long near(50)

		if `x' <  2011 local keepvars fid_2 mrun proceso_dir egreso_dir
		if `x' >= 2011 local keepvars fid_2 mrun proceso_dir

		rename (code_o code)(code code_o)
		merge m:1 code using "${input_path}${dash}mc_`x'"", keepusing(code `keepvars')
		drop if _merge == 2
		drop _merge
		rename (code_o code)(code code_o)
		renvarlab `keepvars', postfix(_o)

		if `y' <  2011 local keepvars code fid_2 mrun proceso_dir egreso_dir
		if `y' >= 2011 local keepvars code fid_2 mrun proceso_dir

		merge m:1 code using "${input_path}${dash}mc_`y'"", keepusing(code `keepvars')
		drop if _merge == 2
		drop _merge

		if `x' <  2011 & `y' <  2011  order mrun_o code_o fid_2_o proceso_dir_o egreso_dir_o km_to_code mrun code fid_2 proceso_dir egreso_dir
		if `x' <  2011 & `y' >= 2011  order mrun_o code_o fid_2_o proceso_dir_o egreso_dir_o km_to_code mrun code fid_2 proceso_dir
		if `x' >= 2011 & `y' <  2011  order mrun_o code_o fid_2_o proceso_dir_o km_to_code mrun code fid_2 proceso_dir egreso_dir
		if `x' >= 2011 & `y' >= 2011  order mrun_o code_o fid_2_o proceso_dir_o km_to_code mrun code fid_2 proceso_dir
		compress
		save "${temp_path}${dash}v`x'_`y'.dta", replace

	}
}
